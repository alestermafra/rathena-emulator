prontera,152,193,5	script	Curandeira	616,{
	.@timetick = gettimetick(2);
	if(@heal_delay > .@timetick) {
		end;
	}
	@heal_delay = .@timetick + 5; // 5 segundos de delay.
	
	OnGetBuffs:
		.@buff_time = 24 * 60 * 60000;
		@eac = eaclass();
		
		if(getbrokenid(1)) {
			repairall;
		}
		
		sc_end SC_STONE;
		sc_end SC_FREEZE;
		sc_end SC_STUN;
		sc_end SC_SLEEP;
		sc_end SC_POISON;
		sc_end SC_CURSE;
		sc_end SC_SILENCE;
		sc_end SC_CONFUSION;
		sc_end SC_BLIND;
		sc_end SC_BLEEDING;
		sc_end SC_HALLUCINATION;
		
		sc_start SC_BLESSING, .@buff_time, 10;
		sc_start SC_INCREASEAGI, .@buff_time, 10;
		sc_start SC_IMPOSITIO, .@buff_time, 10;
		sc_start SC_GLORIA, .@buff_time, 10;
		//sc_start SC_ASSUMPTIO, .@buff_time, 10;
		
		sc_start SC_FOOD_STR_CASH, .@buff_time, 10;
		sc_start SC_FOOD_AGI_CASH, .@buff_time, 10;
		sc_start SC_FOOD_VIT_CASH, .@buff_time, 10;
		sc_start SC_FOOD_DEX_CASH, .@buff_time, 10;
		sc_start SC_FOOD_INT_CASH, .@buff_time, 10;
		sc_start SC_FOOD_LUK_CASH, .@buff_time, 10;
		sc_start SC_SAVAGE_STEAK, .@buff_time,20;
		sc_start SC_COCKTAIL_WARG_BLOOD, .@buff_time,20;
		sc_start SC_MINOR_BBQ, .@buff_time,20;
		sc_start SC_SIROMA_ICE_TEA, .@buff_time,20;
		sc_start SC_DROCERA_HERB_STEAMED, .@buff_time,20;
		sc_start SC_PUTTI_TAILS_NOODLES, .@buff_time,20;

		percentheal 100, 100;
		
		// suprimentos
		.@max = 3000;
		setarray .@stuff[0],
			11548, // Poção Branca de GdE
			11549, // Poção Azul de GdE
			606, // Aloe Vera
			12375, // Acarajé
			14586, // Doce Hiper Açucarado
			12684, // Poção de Ouro
			14542, // Poção Maior de Defesa
			14544, // Poção Maior de Defesa Mágica
			12437, // Suco Celular Enriquecido
			12596, // Doce Mágico
			12217, // Aspersio
			12791, // Pílula de Combate
			12404, // Poção Energizante
			14601, // Bênção de Tyr
			645, // Poção de Concentração
			656, // Poção do Despertar
			657, // Poção da Fúria Selvagem
			12121, // Poção Anti-Vento
			12120, // Poção Anti-Terra
			12119, // Poção Anti-Água
			12118, // Poção Anti-Fogo
			12117, // Conversor Elemental Vento
			12116, // Conversor Elemental Terra
			12114, // Conversor Elemental Fogo
			12115, // Conversor Elemental Água
			12406, // Pergaminho Fantasma
			525; // Panacea
		
		for(.@i = 0; .@i < getarraysize(.@stuff); .@i++) {
			.@item = .@stuff[.@i];
			.@count = countitem(.@item);
			if(.@count < .@max) {
				getitem .@item, .@max - countitem(.@item);
			}
		}
		
		specialeffect2 312;
		dispbottom "[Curandeira] Você está curado.";
		end;
}