-	script	Monstro da Sorte	-1,{
	function killMobs;
	function eventEnd;
	
	OnClock0012:
	OnClock0112:
	OnClock0212:
	OnClock0312:
	OnClock0412:
	OnClock0512:
	OnClock0612:
	OnClock0712:
	OnClock0812:
	OnClock0912:
	OnClock1012:
	OnClock1112:
	OnClock1212:
	OnClock1312:
	OnClock1412:
	OnClock1512:
	OnClock1612:
	OnClock1712:
	OnClock1812:
	OnClock1912:
	OnClock2012:
	OnClock2112:
	OnClock2212:
	OnClock2312:
	OnEventStart:
		.map$ = .maps$[rand(getArraySize(.maps$))];
		killMobs();
		
		monster .map$, 0, 0, "Monstro da Sorte", .mobId, .mobCount, sprintf("%s::OnMonsterKillEvent", strNPCInfo(0));
		announce sprintf("Monstros da sorte foram invocados em %s! [@evento]", .map$), bc_all;
		
		bindatcmd "evento", sprintf("%s::OnAtCommand", strNpcInfo(3));
		end;
		
	OnAtCommand:
		getmapxy .@map$, .@x, .@y;
		if(strcmp(.@map$, "prontera") == 0) {
			if(select("Ir até o mapa do evento:^FF0000Sair^000000") == 1) {
				warp .map$, 0, 0;
			}
		}
		end;
	
	OnMonsterKillEvent:
		announce sprintf("O jogador \"%s\" encontrou o monstro da sorte!", strCharInfo(0)), bc_all;
		getItem 512, 1;
		eventEnd();
		end;
	
	OnInit:
		setarray .maps$[0], "morocc", "geffen", "payon", "alberta", "izlude";
		.mobId = 1002;
		.mobCount = 3;
		killMobs();
		end;
		
	
	// functions	
	function killMobs {
		for(.@i = 0; .@i < getArraySize(.maps$); .@i++) {
			killmonster .maps$[.@i], sprintf("%s::OnMonsterKillEvent", strNPCInfo(0));
		}
	}
	
	function eventEnd {
		unbindatcmd "evento";
		killMobs();
		sleep 5000;
		mapwarp .map$, "prontera", 156, 191;
	}
}

