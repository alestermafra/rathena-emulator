-	script	bg_escolta#control	-1,{
OnInit:
	setarray .winReward[0], 501, 10;
	setarray .loseReward[0], 501, 5;
	mapwarp "bat_b01", "prontera", 156, 191;
	end;
	
OnStart:
	disablenpc "bat_b01-1";
	disablenpc "bat_b01-2";
	enablenpc "healer_bat_b01-1";
	enablenpc "healer_bat_b01-2";
	
	.@queue = getvariableofnpc(.queue, "Batalhas Campais#bg_control");
	.@queueSize = get_queue_size(.@queue);
	
	.atk = bg_create("bat_b01", 10, 290, "bg_escolta#control::OnAtkQuit", "");
	.def = bg_create("bat_b01", 390, 10, "bg_escolta#control::OnDefQuit", "");
	
	queue_shuffle(.@queue);
	
	for(.@i = 0; .@i < .@queueSize; .@i++) {
		.@charId = get_queue_data(.@queue, .@i);
		if(.@i%2 == 0)
			.@team = .atk;
		else
			.@team = .def;
		bg_join(.@team, "bat_b01", 0, 0, .@charId);
	}
	
	clear_queue(.@queue);
	
	// send to waiting room
	bg_warp .atk, "bat_b01", 10, 290;
	bg_warp .def, "bat_b01", 390, 10;
	
	.playerCanEnter = 1; // still allowing players to enter
	initnpctimer;
	end;
	
OnEnterBG:
	if(bg_get_data(.atk, 0) <= bg_get_data(.def, 0)) {
		bg_join(.atk, "bat_b01", 0, 0, getcharid(0));
		warp "bat_b01", 10, 290;
	}
	else {
		bg_join(.def, "bat_b01", 0, 0, getcharid(0));
		warp "bat_b01", 390, 10;
	}
	end;

//OnTimer35000:
OnTimer10000:
	.playerCanEnter = 0; // block players from enter
	mapannounce "bat_b01", "O time defensor deve impedir o avanço do Morroc Abestado!", bc_map;
	end;
	
//OnTimer40000:
OnTimer11000:
	mapannounce "bat_b01", "A batalha começará em 5 segundos", bc_map;
	end;
	
//OnTimer45000:
OnTimer16000:
	bg_warp .atk, "bat_b01", 88, 75;
	bg_warp .def, "bat_b01", 313, 225;
	enablenpc "bat_b01-1";
	enablenpc "bat_b01-2";
	sleep 1000;
	mapannounce "bat_b01", "Comecem!", bc_map;
	donpcevent "Morroc Abestado#bg_escolta::OnStart";
	end;
	
//OnTimer645000: // time's end. def win.
OnTimer76000: // time's end. def win.
	callsub L_GameEnd, .def, .atk;
	end;
	
OnPackageArrived:
	callsub L_GameEnd, .atk, .def;
	end;
	
L_GameEnd:
	stopnpctimer;
	.@winner = getarg(0);
	.@loser = getarg(1);
	.@timeName$ = (.@winner == .atk? "atacante" : "defensor");
	mapannounce "bat_b01", "O Time " + .@timeName$ + " venceu!", bc_map;
	sleep 1000;
	
	bg_get_data .@winner, 1;
	for ( .@i = 0; .@i < $@arenamemberscount; ++.@i )
		getitem .winReward[0], .winReward[1], $@arenamembers[.@i];
	
	bg_get_data .@loser, 1;
	for (.@i = 0; .@i < $@arenamemberscount; ++.@i)
		getitem .loseReward[0], .loseReward[1], $@arenamembers[.@i];
		
	sleep 5000;
	bg_warp .atk, "prontera", 156, 191;
	bg_warp .def, "prontera", 156, 191;
	bg_destroy .atk;
	bg_destroy .def;
	sleep 5000;
	donpcevent "Batalhas Campais#bg_control::OnRotate";
	end;

// "OnQuit" event
OnAtkQuit:  callsub L_Quit, .atk, .def, "atacante";
OnDefQuit: callsub L_Quit, .def, .atk, "defensor";
L_Quit:
	.@team = getarg(0);
	.@another = getarg(1);
	if(!bg_get_data(.@team, 0)) {
		mapannounce "bat_b01", "Todos do time "+ getarg(2) +" saíram!", bc_map, 0xff3333;
		callsub L_GameEnd, .@another, 0;
	}
	end;
}

bat_b01,145,147,3	script	Morroc Abestado#bg_escolta	1917,{
	end;
	
	OnInit:
		setarray .path[0], 145, 147, 157, 161, 171, 171, 182, 185, 198, 193, 213, 190, 224, 182, 237, 171, 248, 160, 259, 151, 273, 150, 285, 150, 300, 149, 313, 149, 326, 149;
		disablenpc;
		end;
	
	OnStart:
		.currentPath = 0;
		.direction = 1;
		set .a, getvariableofnpc(.atk, "bg_escolta#control");
		set .d, getvariableofnpc(.def, "bg_escolta#control");
		enablenpc;
		moveNPC strnpcinfo(0), 145, 147;
	OnTimer1000:
		stopnpctimer;
		getmapxy(.@map$, .@x, .@y, BL_NPC);
		
		viewpointmap .@map$, 1, .@x, .@y, 1, 0xFF0000;
		
		if(.@x == .path[.currentPath] && .@y == .path[.currentPath + 1]) {
			if(.@x == .path[getarraysize(.path) - 2] && .@y == .path[getarraysize(.path) - 1]) {
				// attacker win
				donpcevent "bg_escolta#control::OnPackageArrived";
				misceffect 901;
				sleep 5000;
				disablenpc;
				end;
			}
			
			if(.direction == 1) {
				.currentPath = .currentPath + 2;
			}
			else {
				.currentPath = .currentPath - 2;
				if(.currentPath < 0) {
					.currentPath = 0;
				}
			}
		}
		
		.@hasAttackMember = bg_getareausers(.a, .@map$, .@x-5, .@y-5, .@x+5, .@y+5);
		.@hasDefenseMember = bg_getareausers(.d, .@map$, .@x-5, .@y-5, .@x+5, .@y+5);
		
		if(.@hasAttackMember && !.@hasDefenseMember) {
			if(.direction == 0) {
				.direction = 1;
				.currentPath = .currentPath + 2;
			}
			.@speed = 50;
			if(.@hasAttackMember == 2) {
				.@speed = 600;
			}
			else if(.@hasAttackMember >= 3) {
				.@speed = 500;
			}
			npcspeed .@speed;
			npcwalkto .path[.currentPath], .path[.currentPath + 1];
		}
		else if(!.@hasAttackMember && .@hasDefenseMember) {
			if(.direction == 1) {
				.direction = 0;
				.currentPath = .currentPath - 2;
			}
			npcspeed 1000;
			npcwalkto .path[.currentPath], .path[.currentPath + 1];
		}
		else {
			npcstop;
		}
		
		initnpctimer;
		end;
}

bat_b01,390,10,0	script	healer_bat_b01-1	-1,15,15,{
OnTouch:
	addtimer 1000, "Curandeira::OnGetBuffs";
	end;
}

bat_b01,10,290,0	script	healer_bat_b01-2	-1,15,15,{
OnTouch:
	addtimer 1000, "Curandeira::OnGetBuffs";
	end;
}

bat_b01,10,282,0	warp	bat_b01-1	1,1,bat_b01,88,75
bat_b01,390,17,0	warp	bat_b01-2	1,1,bat_b01,313,225

bat_b01	mapflag	battleground
bat_b01	mapflag	nosave	SavePoint
bat_b01	mapflag	nowarp
bat_b01	mapflag	nowarpto
bat_b01	mapflag	noteleport
bat_b01	mapflag	nomemo
bat_b01	mapflag	nopenalty
bat_b01	mapflag	nobranch
bat_b01	mapflag	noicewall